buildscript {
    repositories { 
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'net.researchgate.release' version '2.8.1'
    id 'com.diffplug.spotless' version '6.11.0'
    id('io.github.gradle-nexus.publish-plugin') version '1.1.0'
}

nexusPublishing {
  repositories {
    sonatype()
  }
}

subprojects {

  apply plugin: 'java-library'
  apply plugin: 'maven-publish'
  apply plugin: 'com.diffplug.spotless'
  apply plugin: 'signing'

  repositories {
      mavenCentral()
  }

  spotless {
    java {
      target 'src/**/*.java'
      importOrder()
      removeUnusedImports()
      googleJavaFormat()
    }
  }

  publishing {

    publications {
       mavenJava(MavenPublication) {

          if (project.name != "micronaut-e2e") {

          pom {

              groupId "io.activated.reduction"
              artifactId "reduction-${project.name}"

              name = 'Reduction ' + project.name

              description = 'Reduction'
              url = 'https://reductionproject.com'

              licenses {
                license {
                  name = 'The Apache License, Version 2.0'
                  url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
              }

              developers {
                developer {
                  id = 'btomasini'
                  name = 'Ben Tomasini'
                  email = 'btomasini@activated.io'
                }
              }
              scm {
                connection = 'scm:git:git://github.com/activatedio/reduction.git'
                developerConnection = 'scm:git:git://github.com/activatedio/reduction.git'
                url = 'https://github.com/activatedio/reduction'
              }
              }

              from components.java
          }

       }
    }

  }

  java {
      sourceCompatibility = JavaVersion.toVersion("11")
      targetCompatibility = JavaVersion.toVersion("11")
      withSourcesJar()
      withJavadocJar()
  }

  dependencies {
      implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.36'
      implementation 'com.google.guava:guava:31.1-jre'
      testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.22.0'
      testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.5.1'
      testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '4.5.1'
      testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.8.2'
      testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.11'
      testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
      testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
  }

  release {
      afterReleaseBuild.dependsOn publish
  }

  signing {
    sign publishing.publications.mavenJava
  }

}

